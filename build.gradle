buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "reverseproxy://172.16.100.98/nexus/content/groups/public/"}
		maven { url "reverseproxy://repo.spring.io/release" }
		maven { url "reverseproxy://repo.spring.io/milestone" }
		maven { url "reverseproxy://repo.spring.io/snapshot" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

jar {
	baseName = 'doorgod-apigateway'
	version = '1.0.0-SNAPSHOT'
}
compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.encoding = 'UTF-8'
}
repositories {
	mavenLocal()
	mavenCentral()
	maven { url "reverseproxy://172.16.100.98/nexus/content/groups/public/"}
	maven { url "reverseproxy://repo.spring.io/release" }
	maven { url "reverseproxy://repo.spring.io/milestone" }
	maven { url "reverseproxy://repo.spring.io/snapshot" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile 'io.vertx:vertx-core:3.3.3'
	compile 'io.vertx:vertx-mongo-client:3.3.3'
	compile 'com.ymatou.disconf:disconf-client:2.6.35'
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile 'com.netflix.hystrix:hystrix-metrics-event-stream:1.5.5'
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	compile 'org.apache.kafka:kafka_2.11:0.10.0.1'
	compile 'com.alibaba:fastjson:1.2.17'
	compile 'io.vertx:vertx-mysql-postgresql-client:3.3.3'
	compile 'com.google.guava:guava:19.0'
	compile ('com.ymatou.errorreporter:errorreporter-logback:1.0.1') {
		exclude group: 'io.netty'
	}
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

configurations {
	compile.exclude module: 'slf4j-log4j12'
}

//复制启动，停止脚本
task copyScript(type: Copy) {
	into "$buildDir/dist"
	from "$projectDir/script"
}

//复制jar
task copyJars(type: Copy,dependsOn:jar) {
	from configurations.runtime  //项目所有的jar依赖
	from "$buildDir/libs"        //本项目jar
	into "$buildDir/dist/lib"
}

//复制配置
task copyConfig(type: Copy){
	from "$projectDir/src/main/resources/"
	into "$buildDir/dist/conf"
}

//打包发布 发布为：gradle release 默认位置为：build/distributions
task release(type: Zip,dependsOn:[copyScript,copyJars,copyConfig]) {
	archiveName="doorgod-apigateway.zip"
	from "$buildDir/dist"
}
